! Name of the expert system: PROFESSIONAL HATS ADVISOR
! Description:  This system assists a user in choosing a hat that would
!               fit thier handsome head and in finding out how it's called.
! Microcomputer used: IBM PC compatible
! Saved file name: Hats.KBS

ENDOFF;
RUNTIME;

! ACTIONS BLOCK
ACTIONS
WOPEN 1,0,0,23,79,2
ACTIVE 1
DISPLAY "



                       PROFESSIONAL HATS CONSULTING APP

                            Developed by FaSciCake
                   under the direction and supervision of
                         Professor Anna Vladimirovna
                      For further information, contact
                          Vyacheslav Fatkhutdinov
                            c/o FaSciCake Labs
							
                            !!! DISCLAIMER !!!
             Eta programma budet ispol'zovat' translit, izvinite!

                           Press any key to continue~"

WCLOSE 1

   DISPLAY "Welcome to the Professional Hats Consulting App!"
   FIND RECO
   DISPLAY " Osnovyvayas na vashyh otvetah,
   my predlagayem vam rassmotret etot variant: {RECO}";



RULE 1
IF      SIZE = BOLSHOI OR SIZE = OBYCHNYJ_RAZMER OR SIZE = MALENKIJ
THEN    SIZE-OK = OK;

RULE 1-1
IF      SIZE = BOLSHOI
THEN    SIZE-CHECK = BIG;

RULE 1-2
IF      SIZE = OBYCHNYJ_RAZMER
THEN    SIZE-CHECK = MED;

RULE 1-3
IF      SIZE = MALENKIJ
THEN    SIZE-CHECK = SML;



RULE 2
IF		SIZE-OK = OK
		AND WRAP = DA OR WRAP = NYET
THEN	WRAP-OK = OK;

RULE 2-1
IF		WRAP = DA AND SIZE-OK = OK
THEN	WRAP-CHECK = DA;

RULE 2-2
IF		WRAP = NYET AND SIZE-OK = OK
THEN	WRAP-CHECK = NYET;



RULE 3
IF		WRAP-OK = OK
		AND WARM = OCHEN OR WARM = OBYCHNO OR WARM = BEZ_UTEPLENIYA
THEN	WARM-OK = OK;

RULE 3-1-1
IF		WRAP-OK = OK
		AND WARM = BEZ_UTEPLENIYA
THEN	WARM-CHECK = BEZ_UTEPLENIYA;

RULE 3-1-2
IF		WRAP-OK = OK
		AND WARM = OBYCHNO
THEN	WARM-CHECK = OBYCHNO;

RULE 3-1-3
IF		WRAP-OK = OK
		AND WARM = OCHEN
THEN	WARM-CHECK = OCHEN;

RULE 3-1
IF		WRAP-OK = OK AND WRAP-CHECK = DA AND WARM = OCHEN
THEN	RECO = Kapor;

RULE 3-2
IF		WRAP-OK = OK AND WRAP-CHECK = DA AND WARM = OBYCHNO
THEN	RECO = Tyurban_ili_Platok;

RULE 3-3-1
IF		WRAP-OK = OK AND WRAP-CHECK = DA AND WARM = BEZ_UTEPLENIYA AND SIZE-CHECK=SML
THEN	RECO = Bandana;

RULE 3-3-2
IF		WRAP-OK = OK AND WRAP-CHECK = DA AND WARM = BEZ_UTEPLENIYA AND SIZE-CHECK=MED
THEN	RECO = Kosynka_ili_Platok;

RULE 3-3-3
IF		WRAP-OK = OK AND WRAP-CHECK = DA AND WARM = BEZ_UTEPLENIYA AND SIZE-CHECK=BIG
THEN	RECO = Platok_ili_Hidzhab;



RULE 4
IF		WARM-OK = OK
		AND VISOR = BEZ_NEGO OR VISOR = KAK_U_KEPKI OR VISOR = KAK_U_PANAMY
THEN	VISOR-OK = OK;

RULE 4-1
IF		WARM-OK = OK AND VISOR = BEZ_NEGO
THEN	VISOR-CHECK = NYET;

RULE 4-2
IF		VISOR = PO_BOKAM_HOCHU
THEN	RECO = Treugolka_ili_Dvuugolka;

RULE 4-3
IF		VISOR = KAK_U_KEPKI
THEN	VISOR-CHECK = VISORFORWARD;

RULE 4-3
IF		VISOR = KAK_U_PANAMY
THEN	VISOR-CHECK = PANAMA;



RULE 5
IF		VISOR-OK = OK
		AND TACKY = DA OR TACKY = OBYCHNO OR TACKY = BROSKO
THEN	TACKY-OK = OK;

RULE 5-1
IF		TACKY = DA
THEN	TACKY-CHECK = SHY;

RULE 5-2
IF		TACKY = OBYCHNO
THEN	TACKY-CHECK = NORMAL;

RULE 5-3
IF		TACKY = BROSKO
THEN	TACKY-CHECK = TACKY;



RULE 4-3-2
IF		TACKY-CHECK = TACKY AND VISOR-CHECK = VISORFORWARD AND PONY = DAAA
		AND SIZE-CHECK = MED OR SIZE-CHECK = SML
THEN	RECO = Ohotnichya_shlyapa;

RULE 4-3-3
IF		TACKY-CHECK = TACKY AND VISOR-CHECK = VISORFORWARD AND PONY = DAAA
		AND SIZE-CHECK = BIG
THEN	RECO = Zhokejka;



RULE 6-1
IF		SPORT = DA AND VISOR-CHECK = VISORFORWARD
		AND TACKY-OK = OK
THEN	RECO = Beysbolka;

RULE 6-2
IF		SPORT = NYET AND VISOR-CHECK = VISORFORWARD
THEN	SPORT-CHECK = NYET;



RULE 7-1
IF		SPORT-CHECK = NYET
		AND FLOOR = MUZHSKOE OR FLOOR = NEVAZHNO
THEN	FLOOR-CHECK = MALE;

RULE 7-2
IF		SPORT-CHECK = NYET
		AND FLOOR = ZHENSKOE
THEN	FLOOR-CHECK = FEM;

RULE 7-3
IF		SPORT-CHECK = NYET AND FLOOR = ZHENSKOE AND VISOR-CHECK = VISORFORWARD
		AND TACKY-OK = OK
THEN	RECO = Kepi;



RULE 8-1
IF		SHAPE = DA
		AND TACKY-OK = OK
THEN	SHAPE-CHECK = DA;

RULE 8-1
IF		SHAPE = DA
		AND TACKY-OK = OK AND VISOR-CHECK = VISORFORWARD
THEN	RECO = Kartuz;

RULE 8-2
IF		SHAPE = NYET
		AND TACKY-OK = OK AND VISOR-CHECK = VISORFORWARD
THEN	RECO = Furazhka;

RULE 8-3
IF		SHAPE = NYET
		AND VISOR-CHECK = VISORFORWARD
THEN	SHAPE-CHECK = NYET;



RULE 9-1
IF		SHAPE-CHECK = DA AND SHLEM = AVE_MARIA_DA
		AND TACKY-OK = OK
THEN	RECO = Ritsarskiy_shlem;



RULE 10-1
IF		FEDORA = DA AND VISOR-CHECK = PANAMA AND TACKY-OK = OK
THEN	FEDORA-CHECK = TRIGGERED;

RULE 10-2
IF		FEDORA = NYET AND VISOR-CHECK = PANAMA AND TACKY-OK = OK
THEN	FEDORA-CHECK = NYET;



RULE 11-1
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Gentleman
THEN	RECO = Cilindr;

RULE 11-2
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Chaplin
THEN	RECO = Kotelok;

RULE 11-3
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Gangsta
THEN	RECO = Homburg_ili_Borsalino;

RULE 11-4
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Landlord
THEN	RECO = Kanotye;

RULE 11-5
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Detective
THEN	RECO = Fedora_classicheskaya;

RULE 11-6
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Oldman
THEN	RECO = Trilbi;

RULE 11-7
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Drouger
THEN	RECO = Fetrovaya_shlyapa;

RULE 11-7
IF		FEDORA-CHECK = TRIGGERED AND LE = Le_Hobo
THEN	RECO = Tvidovaya_shlyapa;



RULE 12-1
IF		PANAMA = DA AND VISOR-CHECK = PANAMA AND TACKY-CHECK = SHY
		AND TACKY-OK = OK
THEN	RECO = Shirokopola_shlyapa;

RULE 12-2
IF		PANAMA = DA AND VISOR-CHECK = PANAMA
		AND TACKY-OK = OK
THEN	PANAMA-CHECK = DA;

RULE 12-3
IF		PANAMA = NYET AND VISOR-CHECK = PANAMA
		AND TACKY-OK = OK
THEN	PANAMA-CHECK = NYET;



RULE 13-1
IF		PANAMA-CHECK = DA AND MODERN = SOVREMENNO
THEN	RECO = Kovboyskaya_shlyapa;

RULE 13-2
IF		PANAMA-CHECK = DA AND MODERN = TRADICIONNO
THEN	RECO = Sombrero;

RULE 13-3
IF		PANAMA-CHECK = DA AND MODERN = TYANET_K_PRIRODE
THEN	RECO = Floppi_ili_prosto_Solomennaya_shlyapa;



RULE 14-1
IF		PANAMA-CHECK = NYET
		AND SMODERN = SOVREMENNO OR SMODERN = NEVAZHNO
THEN	RECO = Panama;

RULE 14-2
IF		PANAMA-CHECK = NYET AND SMODERN = TRADICIONNO
		AND FLOOR = MUZHSKOE OR FLOOR = NEVAZHNO
THEN	RECO = Tirolskaya_shlyapa;

RULE 14-3
IF		PANAMA-CHECK = NYET AND SMODERN = TRADICIONNO
		AND FLOOR = ZHENSKOE
THEN	RECO = Klosh;



RULE 15-1
IF		VISOR-CHECK = NYET AND TACKY-OK = OK
		AND SHAPE-CHECK = NYET
THEN	DIR = TOP;

RULE 15-2
IF		VISOR-CHECK = NYET AND TACKY-OK = OK
		AND SHAPE-CHECK = DA
THEN	DIR = RIGHT;

RULE 15-3
IF		VISOR-CHECK = NYET AND TACKY-OK = OK
		AND SHAPE = NYET AND TACKY-CHECK = SHY
THEN	RECO = Ermolka;

RULE 15-4
IF		VISOR-CHECK = NYET AND TACKY-OK = OK AND SHAPE = NYET
		AND SIZE-CHECK = SML
THEN	RECO = Beret;

RULE 15-5
IF		VISOR-CHECK = NYET AND TACKY-OK = OK AND SHAPE = NYET
		AND SIZE-CHECK = BIG
THEN	RECO = Papaha;

RULE 15-6
IF		VISOR-CHECK = NYET AND TACKY-OK = OK AND SHAPE = NYET
		AND SIZE-CHECK = MED AND WINDY = DA
THEN	RECO = Ushanka_ili_Finka;

RULE 15-7
IF		VISOR-CHECK = NYET AND TACKY-OK = OK AND SHAPE = NYET
		AND SIZE-CHECK = MED AND WINDY = NYET AND EXTRA = NYET
THEN	RECO = Lyzhnaya_shapochka;

RULE 15-8
IF		VISOR-CHECK = NYET AND TACKY-OK = OK AND SHAPE = NYET
		AND SIZE-CHECK = MED AND WINDY = NYET AND EXTRA = DA
THEN	RECO = Slauch;



RULE 16
IF		DIR = RIGHT AND TIGHT = DA
THEN	TIGHT-CHECK = DA;

RULE 16-1
IF		DIR = RIGHT AND TIGHT = NYET AND TACKY-CHECK = SHY OR TACKY = NORMAL
		AND WARM-CHECK = OCHEN OR WARM-CHECK = OBYCHNO
THEN	RECO = Feska;

RULE 16-2
IF		DIR = RIGHT AND TIGHT = NYET AND TACKY-CHECK = TACKY AND SIZE-CHECK = BIG
		AND WARM-CHECK = OCHEN OR WARM-CHECK = OBYCHNO
THEN	RECO = Kasa;

RULE 16-3
IF		DIR = RIGHT AND TIGHT = NYET AND TACKY-CHECK = TACKY AND SIZE-CHECK = MED
		AND WARM-CHECK = OCHEN OR WARM-CHECK = OBYCHNO
THEN	RECO = Tabletka;

RULE 16-4
IF		DIR = RIGHT AND TIGHT = NYET AND TACKY-CHECK = TACKY AND SIZE-CHECK = MED
		AND WARM-CHECK = OCHEN OR WARM-CHECK = BEZ_UTEPLENIYA
THEN	RECO = Korona_ili_Tiara;



RULE 17-1
IF		DIR = RIGHT AND TIGHT = DA
		AND FLOOR-CHECK = MALE OR FLOOR = MUZHSKOE
THEN	RECO = Tyubeteyka;

RULE 17-2
IF		DIR = RIGHT AND TIGHT = DA
		AND FLOOR-CHECK = FEM OR FLOOR = ZHENSKOE
THEN	RECO = Meningitka;

RULE 17-3
IF		DIR = RIGHT AND TIGHT = DA
		AND FLOOR-CHECK = NEVAZHNO OR FLOOR = NEVAZHNO
THEN	RECO = Boyarka_ili_Kubanka;



!STATEMENTS BLOCK

ASK SIZE: " Kakogo razmera vy by hoteli golovnoj ubor?";
CHOICES SIZE: BOLSHOI,OBYCHNYJ_RAZMER,MALENKIJ;

ASK WRAP: " Hotite chto-to namatyvaemoe na golovu?";
CHOICES WRAP: DA,NYET;

ASK WARM: " Hotite chto-to tyoploe?";
CHOICES WARM: OCHEN,OBYCHNO,BEZ_UTEPLENIYA;

ASK VISOR: " Hotite chto-to s kozyrkom?";
CHOICES VISOR: PO_BOKAM_HOCHU,KAK_U_KEPKI,KAK_U_PANAMY,BEZ_NEGO;

ASK TACKY: " Hotite chto-to skromnoye?";
CHOICES TACKY: DA,OBYCHNO,BROSKO;

ASK PONY: " Obozhayete poni?";
CHOICES PONY: DAAA,NYET;

ASK FLOOR: " Muzhskoe ili zhenskoye?";
CHOICES FLOOR: MUZHSKOE,ZHENSKOE,NEVAZHNO;

ASK SPORT: " Hotite chto-to sportivnoye";
CHOICES SPORT: DA,NYET;

ASK SHAPE: " Hotite chto-to chto imelo by formu?";
CHOICES SHAPE: DA,NYET;

ASK SHLEM: " Hotite CO BCEX CTOPOH chtob zakryvalo voobshe?";
CHOICES SHLEM: NYET,AVE_MARIA_DA;

ASK FEDORA: " Hotite chto-to fedoropodobnoye?";
CHOICES FEDORA: DA,NYET;

ASK LE: " Fedora a la..?";
CHOICES LE: Le_Gentleman,Le_Chaplin,Le_Gangsta,Le_Landlord,Le_Detective,Le_Oldman,Le_Drouger,Le_Hobo;

ASK PANAMA: " Hotite prosto gigantskuyu panamu?";
CHOICES PANAMA: DA,NYET;

ASK MODERN: " Posovremenney ili potradicionney?";
CHOICES MODERN: SOVREMENNO,TRADICIONNO,TYANET_K_PRIRODE;

ASK SMODERN: " Posovremenney ili potradicionney?";
CHOICES SMODERN: SOVREMENNO,TRADICIONNO,NEVAZHNO;

ASK WINDY: " Dlya osobo vetrenoy pogody?";
CHOICES WINDY: DA,NYET;

ASK EXTRA: " S dopolnitelnym stilem?";
CHOICES EXTRA: DA,NYET;

ASK TIGHT: " Hotite chtoby plotno sidelo na golove?";
CHOICES TIGHT: DA,NYET;
