! HMO Service Screening Assistant
! with an improved user interface
! Saved as HMOUSER.KBS
!**************************************************************************
BKCOLOR = 3;                     !set screen background color to light blue
RUNTIME;                                 !eliminate rules and facts windows
ENDOFF;                         !eliminate need to press END after a choice

!=============================== Actions Block ============================
ACTIONS
     WOPEN 1,3,10,13,60,7                          !define opening window 1
     ACTIVE 1                                            !activate window 1
     DISPLAY "

              HMO Service Screening Assistant

   This consultation assists you to screen a person who
   enters an HMO facility.  It asks a series of questions
   about the person, then recommends a level of support
   appropriate to the person's needs.

   Please press any key to begin the consultation.~

       "
     WCLOSE 1                                              !remove window 1
     WOPEN 1,1,1,5,77,2                       !define instructions window 1
     ACTIVE 1                                            !activate window 1
     DISPLAY "                           Instructions
          Use the arrow keys to move the lightbar to a desired
          answer choice then press the Enter key."
     WOPEN 2,7,1,14,77,3                      !define consultation window 2
     ACTIVE 2                                            !activate window 2
     FIND support
     WCLOSE 1                                              !remove window 1
     WCLOSE 2                                              !remove window 2
     WOPEN 1,5,13,9,48,7         !define concluding recommendation window 1
     WOPEN 2,6,14,7,46,7              !define window 2 (nested in window 1)
     ACTIVE 2                                            !activate window 2
     LOCATE 1,9                    !specify row and column for next DISPLAY
     DISPLAY "Based on the answers given,
         the recommended support is:
                  {support}.

(Press any key to conclude the consultation.)~"
;
!=============================== Rules Block ==============================

RULE 1
     IF member_status = ok and
        reason = new_case or
        reason = follow_up_case and
        problem = serious
     THEN support = level_1;

RULE 2
     IF member_status = ok and
        reason = new_case and
        problem = non_serious
     THEN support = level_2;

RULE 3
     IF member_status = ok and
        reason = follow_up_case and
        problem = non_serious
     THEN support = level_3;

RULE 4
     IF member_status = ok and
        reason = information_other
     THEN support = information_other;

RULE 5
     IF member_status = not_ok
     THEN support = non_member;

RULE 6
     IF member = yes and
        valid_id = yes
     THEN member_status = ok
     BECAUSE "Validated members are entitled to medical support, while
     non-members are provided with other appropriate support.";

RULE 7
     IF member = yes and
        valid_id = no
     THEN member_status = not_ok;

RULE 8
     IF member = no
     THEN member_status = not_ok;

RULE 9
     IF temperature = normal and
        other_symptoms = yes
     THEN problem = serious
     BECAUSE "The presence of an abnormal temperature or other symptoms
     indicate a serious problem that requires immediate attention.";

RULE 10
     IF temperature = normal and
        other_symptoms = no
     THEN problem = non_serious;

RULE 11
     IF temperature = abnormal or
        temperature = not_known
     THEN problem = serious;

!================================ Questions Block =========================

ASK member: "Is the person an HMO member?";
CHOICES member: yes, no;

ASK valid_id: "Does the person have a valid HMO `id' number?";
CHOICES valid_id: yes, no;

ASK reason: "What is the reason for coming to the HMO facility?";
CHOICES reason: new_case, follow_up_case, information_other;

ASK temperature: "What is the person's temperature?";
CHOICES temperature: normal, abnormal, not_known;

ASK other_symptoms:  "Does the person claim other symptoms?";
CHOICES other_symptoms: yes, no;
