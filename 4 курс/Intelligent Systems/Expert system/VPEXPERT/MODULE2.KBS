! HMO Service Screening Assistant
! Chaining Example: This is the second of two linked knowledge base files.
!                   It is saved as MODULE2.KBS.
!                   It  does not save any variables.
!                   (It links back to MODULE1.KBS.)

!**************************************************************************

!EXECUTE;                                   !start consultation immediately
      !use exclamation point temporarily to disable execute while debugging
BKCOLOR = 3;                     !set screen background color to light blue
RUNTIME;                                 !eliminate rules and facts windows

!============================== Actions Block =============================

ACTIONS
     LOADFACTS tempdata     !load into memory all variable names and values
                              !stored in the temporary file called TEMPDATA
     COLOR = 0                    !set following text color to normal black
     WOPEN 1,1,1,6,77,2                       !define instructions window 1
     ACTIVE 1                                            !activate window 1
     DISPLAY "                           Instructions
          Use the arrow keys to move the lightbar to a desired
          answer choice then press the Enter key.
          Press the End key to move to the next question."
     WOPEN 2,8,1,13,77,3                      !define consultation window 2
     ACTIVE 2                                            !activate window 2
     FIND message                           !set goal to customize messages
     DISPLAY "Press any key to conclude this consultation.~"
     CHAIN module1          !link back to the original MODULE1.KBS to begin
                                  !another consultation session, if desired
;
!============================== Rules Block ===============================

!-------------------- Rule Set 1: Concluding Messages ---------------------

RULE message_level123               !use a meaningful name for a rule label
     IF support = level_1 or
        support = level_2 or
        support = level_3
     THEN message = print_form
        PRINTON                    !send display text to screen and printer
        DISPLAY "Authorization Form For Service - {support}"
        DISPLAY "ID: {id_number}"
        DISPLAY "Reason: {reason}"
        DISPLAY "Self-reported: temperature - {temperature}"
        DISPLAY "               symptoms - {other_symptoms}"
        PRINTOFF                                           !disable printon
        PDISPLAY "Kindly go to {room} for service."         !send text only
                                                                !to printer
        FIND co_pay               !set sub-goal to determine co-pay charges
        EJECT                       !move printer paper to top of next page
        DISPLAY ""                                  !display one blank line
        DISPLAY "Please tear the Authorization Form off the printer.";

        RULE get_co_pay            !single rule to determine co-pay charges
             IF id_number > 999999
             THEN co_pay = yes
                  PDISPLAY"(Co-payment charge of $8.00 made for service.)";

RULE message_non_member
     IF support = non_member
     THEN message = station_Z
        DISPLAY "Appropriate service is available at {message}.";

RULE message_info
     IF support = information_other
     THEN message = display_service
        FIND interest_areas                          !get direct user input
        FIND info_help                  !set sub-goal to customize messages
        DISPLAY "Service is available for plan type / at: {info_help}";

!------------------- Rule Set 2: Information Help Messages ----------------

RULE 2-1
     IF interest_areas = individual_plan
     THEN info_help = individual_station_X;

RULE 2-2
     IF interest_areas = family_plan
     THEN info_help = family_station_Y;

RULE 2-3
     IF interest_areas = co_pay_plan or
        interest_areas = other
     THEN info_help = co_pay_other_station_Z;

!============================= Questions Block ============================

ASK interest_areas: "What kind(s) of information is (are) desired?";
CHOICES interest_areas: individual_plan, family_plan, co_pay_plan, other;

PLURAL: interest_areas, info_help;
